;;; pretty-mode.el --- redisplay parts of the buffer as pretty symbols
;;; -*- coding: utf-8 -*-

;;; Commentary:
;;
;; Minor mode for redisplaying parts of the buffer as pretty symbols
;; originally modified from Trent Buck's version at http://paste.lisp.org/display/42335,2/raw
;; Also includes code from `sml-mode'
;; See also http://www.emacswiki.org/cgi-bin/wiki/PrettyLambda
;;
;; Released under the GPL. No implied warranties, etc. Use at your own risk.
;; Arthur Danskin <arthurdanskin@gmail.com>, March 2008
;;
;; to install:
;; (require 'pretty-mode)
;; and
;; (global-pretty-mode 1)
;; or
;; (add-hook 'my-pretty-language-hook 'turn-on-pretty-mode)

;;; Code:

(eval-when-compile
  (require 'cl))

(defvar pretty-syntax-types '(?_ ?w ?\\))
(defvar pretty-current-point 1)
(defvar pretty-current-line-beginning 1)
(defvar pretty-current-line-end 1)

;; modified from `sml-mode'
(defun pretty-font-lock-compose-symbol (alist)
  "Compose a sequence of ascii chars into a symbol."
  ;; Check that the chars should really be composed into a symbol.
  (let* ((start (match-beginning 0))
         (end (match-end 0))
         (syntax (char-syntax (char-after start))))
    (if (or (and (<= pretty-current-line-beginning start) (>= pretty-current-line-end end))
           (or (if (memq syntax pretty-syntax-types)
               (or (memq (char-syntax (char-before start)) pretty-syntax-types)
                  (memq (char-syntax (char-after end)) pretty-syntax-types))
             (memq (char-syntax (char-before start)) '(?. ?\\)))
           (memq (get-text-property start 'face)
                 '(font-lock-doc-face font-lock-string-face
                                      font-lock-comment-face))))
        ;; No composition for you. Let's actually remove any composition
        ;; we may have added earlier and which is now incorrect.
        (prog1
            nil
          (decompose-region start end))
      ;; That's a symbol alright, so add the composition.
      (prog1
          font-lock-negation-char-face ;; Return a rarely use font-face
        (compose-region start end (cdr (assoc (match-string 0) alist)))))))

(defvar pretty-interaction-mode-alist
  '((inferior-scheme-mode . scheme-mode)
    (lisp-interaction-mode . emacs-lisp-mode)
    (inferior-lisp-mode . lisp-mode)
    (inferior-ess-mode . ess-mode)
    (inf-haskell-mode . haskell-mode)
    (tuareg-interactive-mode . tuareg-mode)
    (inferior-python-mode . python-mode)
    (inferior-octave-mode . octave-mode)
    (inferior-ruby-mode . ruby-mode))
  "Alist mapping from inferior process interaction modes to their
  corresponding script editing modes.")


(defun pretty-font-lock-keywords (alist)
  "Return a `font-lock-keywords' style entry for replacing
regular expressions with symbols. ALIST has the form ((STRING .
REPLACE-CHAR) ...)."
  (when alist
    (let ((rxes (mapcar 'car alist)))
      `((
         ,(rx-to-string `(or ,@rxes))
         (0 (pretty-font-lock-compose-symbol
             ',alist)))))))

(defun pretty-keywords (&optional mode)
  "Return the font-lock keywords for MODE, or the current mode if
MODE is nil. Return nil if there are no keywords."
  (let* ((mode (or mode major-mode))
         (kwds (cdr-safe
                (or (assoc mode pretty-patterns)
                    (assoc (cdr-safe
                            (assoc mode pretty-interaction-mode-alist))
                           pretty-patterns)))))
    (pretty-font-lock-keywords kwds)))

(defgroup pretty nil "Minor mode for replacing text with symbols "
  :group 'faces)

(defun pretty-line-update ()
  "Decompose current line"
  (setq pretty-current-point (point))
  (setq pretty-current-line-beginning (line-beginning-position))
  (setq pretty-current-line-end (line-end-position))
  (font-lock-fontify-buffer))

(define-minor-mode pretty-mode
  "Toggle Pretty minor mode.
With arg, turn Pretty minor mode on if arg is positive, off otherwise.

Pretty mode builds on `font-lock-mode'. Instead of highlighting
keywords, it replaces them with symbols. For example, lambda is
displayed as Î» in lisp modes."
  :group 'pretty
  :lighter " Î»"
  
  (if pretty-mode
      (progn
        (add-hook 'post-command-hook 'pretty-line-update nil t)
        (font-lock-add-keywords nil (pretty-keywords))
        (font-lock-fontify-buffer))
    (remove-hook 'post-command-hook 'pretty-line-update t)
    (font-lock-remove-keywords nil (pretty-keywords))
    (remove-text-properties (point-min) (point-max) '(composition nil))
    (font-lock-fontify-buffer)))

(defun turn-on-pretty-if-desired ()
  "Turn on `pretty-mode' if the current major mode supports it."
  (if (pretty-keywords)
      (pretty-mode 1)))

(define-globalized-minor-mode global-pretty-mode
  pretty-mode turn-on-pretty-if-desired
  :init-value t)

(defun turn-off-pretty-mode ()
  (interactive)
  (pretty-mode -1))


(defun turn-on-pretty-mode ()
  (interactive)
  (pretty-mode +1))

(defun pretty-compile-patterns (patterns)
  "Set pretty patterns in a convenient way.

PATTERNS should be of the form ((GLYPH (REGEXP MODE ...) ...)
...). GLYPH should be a character. MODE should be the name of a
major mode without the \"-mode\". Returns patterns in the form
expected by `pretty-patterns'"
  (let ((pretty-patterns))
    (loop for (glyph . pairs) in patterns do
          (loop for (regexp . major-modes) in pairs do
                (loop for mode in major-modes do
                      (let* ((mode (intern (concat (symbol-name mode)
                                                   "-mode")))
                             (assoc-pair (assoc mode pretty-patterns))

                             (entry (cons regexp glyph)))
                        (if assoc-pair
                            (push entry (cdr assoc-pair))
                          (push (cons mode (list entry))
                                pretty-patterns))))))
    pretty-patterns))

(defvar pretty-patterns nil
"*List of pretty patterns.

Should be a list of the form ((MODE ((REGEXP . GLYPH) ...)) ...)")
(setq pretty-patterns
  (let* ((lispy '(scheme emacs-lisp lisp))
         (mley '(tuareg haskell sml coq))
         (c-like '(c c++ perl sh python java ess ruby))
         (all `(,@lispy ,@mley ,@c-like octave latex)))
    (pretty-compile-patterns
     `(
       (?â‰  ("!=" ,@c-like scheme octave coq)
           ("<>" tuareg octave)
           ("~=" octave)
           ("/=" haskell emacs-lisp)
           ("\\neq" latex))
       (?â‰¤ ("<=" ,@all)
           ("\\leq" latex))
       (?â‰¥ (">=" ,@all)
           ("\\geq" latex))
       (?â‰¡ ("==" ,@all)
           ("\\equiv" latex))
       (?âŸµ ("<-" ,@mley ess)
           ("\\leftarrow" latex))
       (?âŸ¶ ("->" ,@mley ess c c++ perl)
           ("\\rightarrow" latex))
       (?â†‘ ("\\^" tuareg)
           ("^+" coq))
       (?âŸ¹ ("=>" sml perl ruby haskell coq)
           ("\\Rightarrow" latex))
       (?âŸ· ("<->" coq)
           ("\leftrightarrow" latex))
       (?â†£ (">->" coq))
       (?â†¦ ("\\mapsto" latex))
       (?âˆ… ("nil" emacs-lisp ruby)
           ("null" scheme java)
           ("NULL" c c++)
           ("None" python)
           ("set0" coq)
           ("()" ,@mley)
           ("\\emptyset" latex)
           ("\\varnothing" latex))
       (?â€¦ ("\.\.\." scheme)
           ("\.\." haskell)
           ("\\ldots" latex))
       (?âˆˆ ("in" python))
       (?âˆ‰ ("not in" python)
           ("\\notin" coq latex))
       (?âŠ² ("<|" coq))
       (?âˆš ("sqrt" ,@all))
       (?âˆ‘ ("sum" python)
           ("\\sum" coq latex)
           ("\\Sigma" latex))
       (?âˆª (":|:" coq))
       (?âˆ© (":&:" coq))
       (?âˆ ("~:" coq))
       (?Î± ("alpha" ,@all)
           ("'a" ,@mley)
           ("\\alpha" latex))
       (?Î² ("beta" ,@all)
           ("'b" ,@mley)
           ("\\beta" latex))
       (?Î³ ("gamma" ,@all)
           ("'c" ,@mley)
           ("\\gamma" latex))
       (?Î” ("delta" ,@all)
           ("'d" ,@mley)
           ("\\Delta" latex))
       (?Îµ ("epsilon" ,@all)
           ("\\epsilon" latex))
       (?Î¹ ("iota" ,@all)
           ("\\iota" latex))
       (?Î¸ ("theta" ,@all)
           ("\\theta" latex))
       (?Ïƒ ("sigma" ,@all))
       (?Î¼ ("mu" ,@all))
       (?Î» ("lambda" ,@all)
           ("fn" sml)
           ("fun" tuareg)
           ("\\" haskell)
           ("\\lambda" latex))
       (?Ï€ ("pi" ,@all)
           ("M_PI" c c++)
           ("\\pi" latex))
       (?Î  ("Pi" @all)
           ("\\prod" latex)
           ("\\Pi" latex))
       (?Ï‰ ("omega" @all)
           ("\\omega" latex))
       (?Î¦ ("Phi" @all)
           ("\\Phi" latex))
       (?Î© ("Ohm" @all)
           ("\\ohm" latex)
           ("\\Omega" latex))
       (?â„§ ("Mho" @all)
           ("\\mho" latex))
       (?Ï† ("phi" ,@all)
           ("\\varphi" latex))
       (?Î· ("eta" ,@all)
           ("\\eta" latex))

       (?Â² (" ?** ?2" python tuareg octave)
           ("^2" octave haskell))
       (?Â³ (" ?** ?3" python tuareg octave)
           ("^3" octave haskell))
       (?â¿ (" ?** ?n" python tuareg octave)
           ("^n" octave haskell coq))

       (?âˆž ("HUGE_VAL" c c++))
       (?âˆŽ ("Qed." coq))

       (?âˆ™ ("*" python haskell))

       (?âˆ— ("all" python))
       (?âŠ¢ ("assert" python))
       (?â‰ ("is" python))
       (?ðˆ ("filter" python))
       (?ðš· ("map" python))

       (?â‹‚ ("\\bigcap" coq)
           ("\\cap" latex))
       (?âˆ ("\\prod" coq))
       (?â‹ƒ ("\\bigcup" coq)
           ("\\cup" latex))
       (?âŠŽ ("\\uplus" latex))
       (?â„• ("nat" coq))
       (?âˆ£  ("%|" coq))

       (?âˆ§ ("and"     emacs-lisp lisp python)
           ("&&" haskell c c++ java perl coq)
           ("\\wedge" latex)
           ("\\land" latex))
       (?âˆ¨ ("or"      emacs-lisp lisp python)
           ("||" haskell c c++ java perl coq)
           ("\\vee" latex)
           ("\\lor" latex))

       (?Â¬ ("not ?" python lisp emacs-lisp haskell)
           ("!" c c++ java)
           ("~~" coq)
           ("\\neg" latex))
       (?âŠ» ("(+)" coq))
       (?âˆ€ ("for" python)
           ("forall" haskell coq)
           ("\\forall" latex))
       (?âˆ„ ("not any" python))
       (?âˆƒ ("any" python)
           ("exists" coq)
           ("\\exists" latex))
       (?âŠ‚ ("\\proper" coq)
           ("\\subset" latex))
       (?âŠ† ("\\subset" coq)
           ("\\subseteq" latex))
       (?âˆ– (":\\:" coq)
           ("\\setminus" latex))
       (?â‹Š ("><|" coq))

       (?Ã— ("\\times" latex))
       (?ã€ˆ ("\\langle" latex))
       (?ã€‰ ("\\rangle" latex))
       (?â‰» ("\\succ" latex))
       (?â‰º ("\\prec" latex))))))


(defun pretty-add-keywords (mode keywords)
  "Add pretty character KEYWORDS to MODE

MODE should be a symbol, the major mode command name, such as
`c-mode' or nil. If nil, pretty keywords are added to the current
buffer. KEYWORDS should be a list where each element has the
form (REGEXP . CHAR). REGEXP will be replaced with CHAR in the
relevant buffer(s)."
  (font-lock-add-keywords
   mode (mapcar (lambda (kw) `(,(car kw)
                          (0 (prog1 nil
                               (compose-region (match-beginning 0)
                                               (match-end 0)
                                               ,(cdr kw))))))
                keywords)))

(defun pretty-regexp (regexp glyph)
  "Replace REGEXP with GLYPH in buffer."
  (interactive "MRegexp to replace:
MCharacter to replace with: ")
  (pretty-add-keywords nil `((,regexp . ,(string-to-char glyph))))
  (font-lock-fontify-buffer))

(provide 'pretty-mode)
